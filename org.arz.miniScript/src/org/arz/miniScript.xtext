grammar org.arz.MiniScript with org.eclipse.xtext.common.Terminals

generate miniScript "http://www.arz.org/miniScript"

Model:
	Program;

Program:
	expressions+=Expression ';' (expressions+=Expression ';')*
;

Expression:
 	FunctionDeclaration  | 
    VariableAssignment |
	NumericExpression
;


FunctionDeclaration:
	'func' '(' (parameters+=ID (',' parameters+=ID)*)? ')' body=Body
;

Body :
 	expressions+=Expression |
    {Body}'{' ( expressions+=Expression ';')* '}'
;

Apply returns Expression:
	Functor ({Apply.functor=current} '(' (arguments+=Expression (',' arguments+=Expression)*)? ')')*
;

Functor returns Expression:
	SymbolReference |
	ParenthesisExpression
;


LiteralExpr :
	value=INT
;

SymbolReference :
	id=ID
;

enum AdditionOperator : add='+' | subtract='-' ;
enum FactorOperator:  mult='*' | division='/';

NumericExpression returns Expression:
	Factor ({NumericExpression.leftFactor=current} operator=AdditionOperator rightFactor=Factor)*
;

Factor returns Expression: 
	Term ({Factor.leftTerm=current} operator=FactorOperator rightTerm=Term)*
;

Term returns Expression:
 	LiteralExpr | Apply ;

VariableAssignment :
	id=ID '=' expression=Expression
;

ParenthesisExpression returns Expression:
	'(' Expression ')'
;


